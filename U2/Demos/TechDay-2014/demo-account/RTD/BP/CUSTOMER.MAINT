*******************************************************************************
*       Program CUSTOMER.MAINT
*       Copyright (C) 2014
*******************************************************************************
*  Author:		Michael Byrne
*  Created on:	Jun 16, 2014
*  Description:	Customer maintenance screen for Rocket Tech Day training
*****************************************************************************
SUBROUTINE CUSTOMER.MAINT(ID, RTN)
	$INCLUDE RTD.INCLUDE RTD.H
	CALL FILE.OPENS
	
	$INCLUDE UNIVERSE.INCLUDE ATFUNCTIONS.H
	$INCLUDE UNIVERSE.INCLUDE TERMINFO
	
	PROMPT ""
	DIM CUST(50)

	GOSUB READ.DATA
	GOSUB DISP.FORM
	GOSUB DISP.DATA
	GOSUB GET.ACTION
RETURN

DISP.FORM:
	PRINT CLS
	GOSUB DISP.NAV
	PRINT @(IT$SBOLD):
	PRINT @(1,2):"First Name:"
	PRINT @(1,3):"Last Name:"
	PRINT @(1,4):"Address:"
	PRINT @(1,5):"City:"
	PRINT @(1,6):"State:"
	PRINT @(1,7):"Zip:"
	PRINT @(1,8):"Email:"
	PRINT @(IT$SLINEGRFX)
	FOR I = 1 TO 78
		PRINT @(I,11):@(IT$LINEGRFXCH,IT$GRFX.H.LINE)
	NEXT
	PRINT @(IT$ELINEGRFX)
	PRINT @(37,11):"Notes"
	PRINT @(40,2):"Home Phone:"
	PRINT @(40,3):"Cell Phone:"
	PRINT @(40,5):"Birthdate:"
	PRINT @(40,6):"Status:"
	PRINT @(40,8):"Sales/Cost:"
	PRINT @(IT$EBOLD):
RETURN

DISP.NAV:
	PRINT @(IT$SREV)
	TITLE = " CUSTOMER MAINTENANCE "
	TLEFT = 40-(LEN(TITLE)/2)
	PRINT @(TLEFT,0):TITLE
	PRINT @(IT$EREV)
	PRINT @(IT$SBOLD)
	PRINT @(0,0):"100.2"
	PRINT @(1,23):"Esc-Cancel  F2-Save  F3-Edit  F4-Add Note":
	PRINT @(IT$EBOLD):
RETURN

DISP.NOTES:
	GOSUB READ.NOTES
	NROW = 12
	FOR NIDX = 1 TO DCOUNT(NOTES,@AM)
		NROW += 1
		IF NROW > 21 THEN EXIT
		PRINT @(IT$SBOLD):@(0,NROW):OCONV(NOTES<NIDX,1>,'D2/'):@(IT$EBOLD):
		NOTE = NOTES<NIDX,3>
		NOTEWIDTH = 69
		IF LEN(NOTE) > NOTEWIDTH THEN
			ROWCNT = INT(LEN(NOTE)/NOTEWIDTH)+1
			IF MOD(ROWCNT,NOTEWIDTH)=0 THEN ROWCNT -= 1
			STARTCHAR = 1
			FOR NIDX2 = 0 TO ROWCNT-1
				IF NIDX2 > 0 THEN 
					NROW += 1
					STARTCHAR += NOTEWIDTH
				END
				IF NROW > 21 THEN EXIT
				PRINT @(9,NROW):NOTE[STARTCHAR,NOTEWIDTH]:
			NEXT
		END ELSE
			PRINT @(9,NROW):NOTE
		END
		IF NROW > 21 THEN EXIT
	NEXT
RETURN

DISP.DATA:
	PRINT @(13,2):CUST(2)
	PRINT @(13,3):CUST(1)
	PRINT @(13,4):CUST(5)
	PRINT @(13,5):CUST(6)
	PRINT @(13,6):CUST(7)
	PRINT @(13,7):CUST(8)
	PRINT @(13,8):CUST(13)
	PRINT @(55,2):CUST(11)
	PRINT @(55,3):CUST(12)
	PRINT @(55,5):OCONV(CUST(10),"D4/")
	PRINT @(55,6):CUST(17)
	PRINT @(55,8):OCONV(CUST(25),"MD2$"):' / ':OCONV(CUST(26),"MD2$")
	GOSUB DISP.NOTES
RETURN

GET.ACTION:
	KEY = ''
	CODE = -999
	DONE = 0
	LOOP UNTIL UPCASE(KEY)="Q"
		PRINT @(0,23):CURSOR.INVISIBLE:
		CALL *GET.TA.BUF.B(1,5,25,50,KEY)
		BEGIN CASE
			CASE KEY = KEY.FUNCTION.2
				GOSUB WRITE.DATA
				RETURN
			CASE KEY = KEY.FUNCTION.3
				GOSUB GET.NEWVALS
			CASE KEY = KEY.FUNCTION.4
				GOSUB ADD.NOTE
				GOSUB DISP.FORM
				GOSUB DISP.DATA
			CASE KEY = CHAR(27)
				RETURN
		END CASE
	REPEAT
RETURN

GET.NEWVALS:
	INPUT @(13,2) CUST(2)
	INPUT @(13,3) CUST(1)
	INPUT @(13,4) CUST(5)
	INPUT @(13,5) CUST(6)
	INPUT @(13,6) CUST(7)
	INPUT @(13,7) CUST(8)
	INPUT @(13,8) CUST(13)
	INPUT @(55,2) CUST(11)
	INPUT @(55,3) CUST(12)
	DATETEMP = OCONV(CUST(10),"D4/")
	INPUT @(55,5) DATETEMP
	CUST(10) = ICONV(DATETEMP, "D4/")
	INPUT @(55,6):CUST(17)
RETURN

ADD.NOTE:
	PRINT CLS
	PRINT "Please add note text below:"
	INPUT NOTE.TEXT
	SEQ.NOTE = DCOUNT(NOTES,AM)+1
	NOTE = DATE():AM:TIME():AM:NOTE.TEXT
	NOTE.ID = ID:'*':SEQ.NOTE
	
	WRITE NOTE ON FILES(F.NOTES), NOTE.ID ON ERROR
		PRINT "(ERROR) Unable to write record..."
		INPUT JUNK,1
	END THEN
		GOSUB READ.NOTES
	END
RETURN

READ.NOTES:	
	NOTES = ''	
	EXECUTE 'SELECT NOTES WITH CUST.ID = ':ID CAPTURING OUT
	
	LOOP
		READNEXT NOTE.ID ELSE EXIT
		
		READ NOTE FROM FILES(F.NOTES),NOTE.ID THEN
			NOTES<-1> = LOWER(NOTE)
		END
	REPEAT
RETURN

READ.DATA:
	ERR.MSG = ""
	MATREAD CUST FROM FILES(F.CUSTOMERS),ID ELSE
		ERR.MSG = "Unable to read member ID ":ID
		RETURN
	END
	
RETURN

WRITE.DATA:
	ERR.MSG = ""
	MATWRITE CUST ON FILES(F.CUSTOMERS),ID ELSE
		ERR.MSG = "Unable to write member ID ":ID
		RETURN
	END
	
RETURN

RETURN

